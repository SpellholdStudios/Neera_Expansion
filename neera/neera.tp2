/* ***************
 *   Mod setup   *
 * ***************/
BACKUP ~neera/backup~
//AUTHOR ~K'aeloree (kae@spellholdstudios.net)~
SUPPORT ~K'aeloree and Spellhold Studios at http://www.shsforums.net/topic/60850-neera-expansion-updated-to-v120/~

VERSION ~v1.3.0~

README ~neera/readme/neera-expansion-readme-english.html~

// MODDER

ALWAYS
	ACTION_IF GAME_IS ~bgee~ BEGIN
		INCLUDE ~%MOD_FOLDER%/lib/liam_bgee_vars.tpa~
		OUTER_SET strref_add = 0
	END ELSE ACTION_IF GAME_IS ~eet~ BEGIN
		INCLUDE ~%MOD_FOLDER%/lib/eet_cpmvars.tpa~
		OUTER_SET strref_add = 200000
	END
END

AUTO_TRA ~neera/lang/%s~

LANGUAGE ~English~
         ~english~
         ~neera/lang/english/setup.tra~

LANGUAGE ~Russian (Translation by Austin)~
         ~russian~
         ~neera/lang/russian/setup.tra~


/* ===================== *
 *    Neera Expansion    *
 * ===================== */
BEGIN @2                                // ~Neera Expansion~
DESIGNATED 0 LABEL ~neera_expansion~
GROUP @1                                // ~Neera Expansion content~
REQUIRE_PREDICATE GAME_IS ~bgee eet~ @3 // ~You must have Baldur's Gate: Enhanced Edition installed.~

// Appending gtimes.ids to include the timer
APPEND ~gtimes.ids~ ~5400 LK#NEERA_TIMER~ UNLESS ~LK#NEERA_TIMER~

// Compile dialogues
COMPILE EVALUATE_BUFFER ~neera/dialogue/neeraj.d~
COMPILE EVALUATE_BUFFER ~neera/dialogue/flirts.d~

// Compile scripts
EXTEND_BOTTOM ~%NEERA_BCS%.bcs~ ~neera/scripts/neera.baf~ EVALUATE_BUFFER
EXTEND_BOTTOM ~neerad%eet_var%.bcs~ ~neera/scripts/neerad.baf~ EVALUATE_BUFFER

/* ===================== *
 *    Talks intervals    *
 * ===================== */
BEGIN @5                                // ~15 minutes~
DESIGNATED 1 LABEL ~neera_expansion_talks_900~
REQUIRE_COMPONENT ~neera.tp2~ ~0~ @10   // ~You need the Neera Expansion installed!~
SUBCOMPONENT @4                         // ~How much time would you like between talks (approximately)?~
GROUP @1                                // ~Neera Expansion content~

COPY_EXISTING ~gtimes.ids~ ~override~
	REPLACE_TEXTUALLY ~5400 LK#NEERA_TIMER~ ~900 LK#NEERA_TIMER~

BEGIN @6                                // ~30 minutes~
DESIGNATED 2 LABEL ~neera_expansion_talks_1800~
REQUIRE_COMPONENT ~neera.tp2~ ~0~ @10   // ~You need the Neera Expansion installed!~
SUBCOMPONENT @4                         // ~How much time would you like between talks (approximately)?~
GROUP @1                                // ~Neera Expansion content~

COPY_EXISTING ~gtimes.ids~ ~override~
	REPLACE_TEXTUALLY ~5400 LK#NEERA_TIMER~ ~1800 LK#NEERA_TIMER~

BEGIN @7                                // ~1 hour~
DESIGNATED 3 LABEL ~neera_expansion_talks_3600~
REQUIRE_COMPONENT ~neera.tp2~ ~0~ @10   // ~You need the Neera Expansion installed!~
SUBCOMPONENT @4                         // ~How much time would you like between talks (approximately)?~
GROUP @1                                // ~Neera Expansion content~

COPY_EXISTING ~gtimes.ids~ ~override~
	REPLACE_TEXTUALLY ~5400 LK#NEERA_TIMER~ ~3600 LK#NEERA_TIMER~
  
BEGIN @8                                // ~1.5 hours (default)~
DESIGNATED 4 LABEL ~neera_expansion_talks_5400~
REQUIRE_COMPONENT ~neera.tp2~ ~0~ @10   // ~You need the Neera Expansion installed!~
SUBCOMPONENT @4                         // ~How much time would you like between talks (approximately)?~
GROUP @1                                // ~Neera Expansion content~

BEGIN @9                                // ~2 hours~
DESIGNATED 5 LABEL ~neera_expansion_talks_7200~
REQUIRE_COMPONENT ~neera.tp2~ ~0~ @10   // ~You need the Neera Expansion installed!~
SUBCOMPONENT @4                         // ~How much time would you like between talks (approximately)?~
GROUP @1                                // ~Neera Expansion content~

COPY_EXISTING ~gtimes.ids~ ~override~
	REPLACE_TEXTUALLY ~5400 LK#NEERA_TIMER~ ~7200 LK#NEERA_TIMER~
  

/* ========================= *
 *    Game romance tweaks    *
 * ========================= */

/* --------------------------------- *
 *  Make Neera romanceable by women  *
 * --------------------------------- */
BEGIN @12                               // ~Make NEERA romanceable by women~
DESIGNATED 6 LABEL ~neera_romanceable_by_women~
REQUIRE_COMPONENT ~neera.tp2~ ~0~ @10   // ~You need the Neera Expansion installed!~
GROUP @11                               // ~Game romance tweaks~

COPY_EXISTING ~%NEERA_BCS%.bcs~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY ~Gender(Player1,MALE)~ ~~
	END
BUT_ONLY_IF_IT_CHANGES

ACTION_IF GAME_IS ~bgee~ BEGIN
	OUTER_SET strref_28035 = 28035
	OUTER_SET strref_28036 = 28036
	OUTER_SET strref_28027 = 28027
	OUTER_SET strref_28028 = 28028
	OUTER_SET strref_28029 = 28029
END ELSE ACTION_IF GAME_IS ~eet~ BEGIN
	OUTER_SET strref_28035 = 228035
	OUTER_SET strref_28036 = 228036
	OUTER_SET strref_28027 = 228027
	OUTER_SET strref_28028 = 228028
	OUTER_SET strref_28029 = 228029
END

COMPILE EVALUATE_BUFFER ~neera/dialogue/neera_romance.d~

// Loads functions library dealing with dlg.
INCLUDE ~%MOD_FOLDER%/lib/gw_dlg_functions.tpa~

// Looking for state using strref @42 (~If I didn't know better, mister adventurer sir, I'd say you were flirting with me.~)
// -------------------------------------------------------------------------------------------------------------------------
/*OUTER_SET neeraj_say42 = STATE_WHICH_SAYS 42 IN ~neera/lang/%s/flirts.tra~ FROM ~%NEERA_JOINED%~
PRINT ~DEVPT CONTROL Found @42 in %NEERA_JOINED%.dlg for REPLACE_TEXTUALLY: state #%neeraj_say42%.~*/
WITH_TRA ~neera/lang/%s/flirts.tra~  BEGIN
	OUTER_SET neera42_strref = RESOLVE_STR_REF(@42)
//	PRINT "neera42_strref = %neera42_strref%"
END
WITH_TRA ~neera/lang/%s/gameromancestweaks.tra~  BEGIN
COPY_EXISTING ~%NEERA_JOINED%.dlg~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY ~SAY #%neera42_strref%~ ~SAY @99942~
	END
END

// Looking for response using strref #28047 (~You know I'm a man of the world. I'm just saying I've had better.~).
// ---------------------------------------------------------------------------------------------------------------
COPY_EXISTING - ~%NEERA_JOINED%.dlg~ ~override~
	PATCH_IF (SOURCE_SIZE > 0x2f) THEN BEGIN	// protects against invalid files
		LPF ~GW_FIND_DLG_RESPONSE_STRING~ INT_VAR GW_string_dlg = 28047 + strref_add RET GW_transition_found GW_state_number GW_transition_number GW_response_index END
		PATCH_IF ("%GW_transition_found%" STRING_EQUAL "Y") AND (GW_transition_number != "-99") BEGIN
			SET neera_transition28047 = GW_transition_number
			SET neera_state28047 = GW_state_number
//			PATCH_PRINT "DEVPT CONTROL %SOURCE_RES%.dlg	-	Transition found = %GW_transition_found%	-	state = #%GW_state_number%	-	transition = #%GW_transition_number% => response index = %GW_response_index%."
		END
	END	// of PATCH_IF (SOURCE_SIZE > 0x2f)
BUT_ONLY

// Replacing strref #28047 with @28047 (~You know I'm a traveler of the world. I'm just saying I've had better.~) and @42 with @99942 (~If I didn't know better, my good adventurer, I'd say you were flirting with me.~)
// ----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
ACTION_IF neera_state28047 >= 0 BEGIN	// Compiles only in case STATE_WHICH_SAYS throws a valid value
	<<<<<<<< ...inlined/neeraromtweak.d
ALTER_TRANS %NEERA_JOINED%
BEGIN %neera_state28047% END		// state number (can be more than one)
BEGIN %neera_transition28047% END	// transition number (can be more than one)
BEGIN								// list of changes
	"REPLY" ~@28047~
END
>>>>>>>>
	COMPILE EVALUATE_BUFFER ~...inlined/neeraromtweak.d~ USING ~neera/lang/%s/gameromancestweaks.tra~
END


/* -------------------------------- *
 *  Make Rasaad romanceable by men  *
 * -------------------------------- */
BEGIN @13                               // ~Make Rasaad romanceable by men~
DESIGNATED 7 LABEL ~rasaad_romanceable_by_men~
REQUIRE_PREDICATE GAME_IS ~bgee eet~ @3 // ~You must have Baldur's Gate: Enhanced Edition installed.~
GROUP @11                               // ~Game romance tweaks~

COPY_EXISTING ~%RASAAD_BCS%.bcs~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY ~Gender(Player1,FEMALE)~ ~~
	END
BUT_ONLY_IF_IT_CHANGES

// Loads functions library dealing with dlg.
INCLUDE ~%MOD_FOLDER%/lib/gw_dlg_functions.tpa~

// Looking for response using strrefs #30920, 30925, 30930 and 30935 (~But you also think I'm radiantly beautiful, right?~)
// ------------------------------------------------------------------------------------------------------------------------
COPY_EXISTING - ~%RASAAD_JOINED%.dlg~ ~override~
	PATCH_IF (SOURCE_SIZE > 0x2f) THEN BEGIN	// protects against invalid files
		LPF ~GW_FIND_DLG_RESPONSE_STRING~ INT_VAR GW_string_dlg = 30920 + strref_add RET GW_transition_found GW_state_number GW_transition_number GW_response_index END
		PATCH_IF ("%GW_transition_found%" STRING_EQUAL "Y") AND (GW_transition_number != "-99") BEGIN
			SET rasaad_transition30920 = GW_transition_number
			SET rasaad_state30920 = GW_state_number
//			PATCH_PRINT "DEVPT CONTROL %SOURCE_RES%.dlg	-	Transition found = %GW_transition_found%	-	state = #%GW_state_number%	-	transition = #%GW_transition_number%  => response index = %GW_response_index%."
		END
		LPF ~GW_FIND_DLG_RESPONSE_STRING~ INT_VAR GW_string_dlg = 30925 + strref_add RET GW_transition_found GW_state_number GW_transition_number GW_response_index END
		PATCH_IF ("%GW_transition_found%" STRING_EQUAL "Y") AND (GW_transition_number != "-99") BEGIN
			SET rasaad_transition30925 = GW_transition_number
			SET rasaad_state30925 = GW_state_number
//			PATCH_PRINT "DEVPT CONTROL %SOURCE_RES%.dlg	-	Transition found = %GW_transition_found%	-	state = #%GW_state_number%	-	transition = #%GW_transition_number%  => response index = %GW_response_index%."
		END
		LPF ~GW_FIND_DLG_RESPONSE_STRING~ INT_VAR GW_string_dlg = 30930 + strref_add RET GW_transition_found GW_state_number GW_transition_number GW_response_index END
		PATCH_IF ("%GW_transition_found%" STRING_EQUAL "Y") AND (GW_transition_number != "-99") BEGIN
			SET rasaad_transition30930 = GW_transition_number
			SET rasaad_state30930 = GW_state_number
//			PATCH_PRINT "DEVPT CONTROL %SOURCE_RES%.dlg	-	Transition found = %GW_transition_found%	-	state = #%GW_state_number%	-	transition = #%GW_transition_number%  => response index = %GW_response_index%."
		END
		LPF ~GW_FIND_DLG_RESPONSE_STRING~ INT_VAR GW_string_dlg = 30935 + strref_add RET GW_transition_found GW_state_number GW_transition_number GW_response_index END
		PATCH_IF ("%GW_transition_found%" STRING_EQUAL "Y") AND (GW_transition_number != "-99") BEGIN
			SET rasaad_transition30935 = GW_transition_number
			SET rasaad_state30935 = GW_state_number
//			PATCH_PRINT "DEVPT CONTROL %SOURCE_RES%.dlg	-	Transition found = %GW_transition_found%	-	state = #%GW_state_number%	-	transition = #%GW_transition_number%  => response index = %GW_response_index%."
		END
	END	// of PATCH_IF (SOURCE_SIZE > 0x2f)
BUT_ONLY

// Replacing strref #30920, 30925, 30930 and 30935 with @30920, 30925, 30930 and 30935 (~But you also think I'm incredibly good-looking, right?~)
// ----------------------------------------------------------------------------------------------------------------------------------------------
ACTION_IF rasaad_state30920 >= 0 BEGIN	// Compiles only in case STATE_WHICH_SAYS throws a valid value
	<<<<<<<< ...inlined/rasaadromtweak.d
ALTER_TRANS %RASAAD_JOINED%
BEGIN %rasaad_state30920% END		// state number (can be more than one)
BEGIN %rasaad_transition30920% END	// transition number (can be more than one)
BEGIN								// list of changes
	"REPLY" ~@30920~
END
ALTER_TRANS %RASAAD_JOINED%
BEGIN %rasaad_state30925% END		// state number (can be more than one)
BEGIN %rasaad_transition30925% END	// transition number (can be more than one)
BEGIN								// list of changes
	"REPLY" ~@30925~
END
ALTER_TRANS %RASAAD_JOINED%
BEGIN %rasaad_state30930% END		// state number (can be more than one)
BEGIN %rasaad_transition30930% END	// transition number (can be more than one)
BEGIN								// list of changes
	"REPLY" ~@30930~
END
ALTER_TRANS %RASAAD_JOINED%
BEGIN %rasaad_state30935% END		// state number (can be more than one)
BEGIN %rasaad_transition30935% END	// transition number (can be more than one)
BEGIN								// list of changes
	"REPLY" ~@30935~
END
>>>>>>>>
	COMPILE EVALUATE_BUFFER ~...inlined/rasaadromtweak.d~ USING ~neera/lang/%s/gameromancestweaks.tra~
END
